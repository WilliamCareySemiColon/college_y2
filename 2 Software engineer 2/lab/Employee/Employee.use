-- $ProjectHeader: use 0.393 Wed, 16 May 2007 14:10:28 +0200 opti $

-- Example illustrating pre- and postconditions

model Employee

-- classes

class Person
attributes
  name : String
  age : Integer
  salary : Real
operations
  raiseSalary(rate : Real) : Real
end

class Company
attributes
  name : String
  location : String
operations
  hire(p : Person)
  fire(p : Person)
end

-- associations

association WorksFor between
  Person[*] role employee
  Company[0..1] role employer
end

-- constraints

constraints

context Person::raiseSalary(rate : Real) : Real
  pre personAge : age > 20
  post raiseSalaryPost: 
    salary = salary@pre * (1.0 + rate)
  post resultPost:
    result = salary

context Company::hire(p : Person)
  pre  hirePre1: p.isDefined()
  pre  hirePre2: employee->excludes(p)
  post hirePost: employee->includes(p)
  post hirePost2: p.salary >= 20000

context Company::fire(p : Person)
  pre  firePre:  employee->includes(p)
  pre firePre2: p.salary < 100000
  post firePost: employee->excludes(p)
  post firePost2: p.salary = 0

